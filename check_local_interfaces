#!/usr/bin/perl

# Copyright (c) 2014, Evgeni Golov
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without modification,
# are permitted provided that the following conditions are met:
#
# * Redistributions of source code must retain the above copyright notice, this
#   list of conditions and the following disclaimer.
#
# * Redistributions in binary form must reproduce the above copyright notice, this
#   list of conditions and the following disclaimer in the documentation and/or
#   other materials provided with the distribution.
#
# * Neither the name of the {organization} nor the names of its
#   contributors may be used to endorse or promote products derived from
#   this software without specific prior written permission.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
# ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
# WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
# DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE FOR
# ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
# (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
# LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON
# ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
# (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
# SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

use strict;
use warnings;
use Nagios::Plugin;
use Storable qw(store retrieve freeze thaw dclone);

my $has_number_format = eval "use Number::Format; 1" ? 1 : 0;

my $proc    = '/proc/net/dev';
my $netstat = 'netstat -ibdWn';
my $tmpfile = $ENV{"HOME"} . '/check_local_interfaces.tmp';
my $now     = time;
my $sleep   = -1;

my $np = Nagios::Plugin->new(
    usage => "Usage: %s [ -i|--interface=<interface>] "
      . "[ -s|--sleep=<seconds> ] "
      . "[ -w|--warnrxbytes=<bytes> ] "
      . "[ -c|--critrxbytes=<bytes> ] "
      . "[ -W|--warntxbytes=<bytes> ] "
      . "[ -C|--crittxbytes=<bytes> ] "
      . "[ -e|--warnerrs=<errors> ] "
      . "[ -E|--criterrs=<errors> ] "
      . "[ -u|--units=<units> ] "
      . "[ -v|--verbose ]",
    shortname => 'Check local interface stats',
    url       => 'https://github.com/evgeni/check_local_interfaces',
    version   => '0.1',
    license   => 'This plugin is free software, and comes with ABSOLUTELY
NO WARRANTY. It may be used, redistributed and/or modified under
the terms of the BSD 3-clause license.',
);

$np->add_arg(
    spec => 'interface|i=s@',
    help =>
'Interface to be monitored, can be given multiple times. Defaults to ALL interfaces.',
);

$np->add_arg(
    spec    => 'sleep|s=i',
    help    => 'Sleep between taking samples (default: %s)',
    default => 5,
);

$np->add_arg(
    spec => 'warnrxbytes|w=i',
    help =>
'WARNING if more than X bytes/s were received during sample period (default: %s)',
    default => 0,
);

$np->add_arg(
    spec => 'critrxbytes|c=i',
    help =>
'CRITICAL if more than X bytes/s were received during sample period (default: %s)',
    default => 0,
);

$np->add_arg(
    spec => 'warntxbytes|W=i',
    help =>
'WARNING if more than X bytes/s were sent during sample period (default: %s)',
    default => 0,
);

$np->add_arg(
    spec => 'crittxbytes|C=i',
    help =>
'CRITICAL if more than X bytes/s were sent during sample period (default: %s)',
    default => 0,
);

$np->add_arg(
    spec => 'warnerrs|e=i',
    help =>
'WARNING if more than X errors/s were seen during sample period (default: %s)',
    default => 0,
);

$np->add_arg(
    spec => 'criterrs|E=i',
    help =>
'CRITICAL if more than X errors/s were seen during sample period (default: %s)',
    default => 0,
);

$np->add_arg(
    spec => 'units|u=s',
    help =>
'Output speeds in units/second (default: %s, needs Number::Format for everything else)',
    default => 'bytes',
);

$np->add_arg(
    spec => 'tmpfile|T:s',
    help => 'Read / write temporary data from / to <tmpfile> (default: '
      . $tmpfile . ')',
);

$np->getopts;

if ( defined $np->opts->tmpfile ) {
    if ( $np->opts->tmpfile ne "" ) {
        $tmpfile = $np->opts->tmpfile;
    }
}

##
## write_tmpfile(filename, timestamp, stats_hash)
##
## Return 1 in case of a successful write. Otherwise
## the exit the program with UNKNOWN state.
##
sub write_tmpfile {
    my ( $filename, $time, %stats ) = @_;

    $stats{'time'} = time;
    store( \%stats, $filename )
      or
      $np->nagios_exit( 'UNKNOWN', "could not store stat in file: $filename" );

    return 1;
}

##
## read_tmpfile(filename)
##
## Returns a tuple of 3 values. (return_code, timestamp, stats_hash).
## return_code contains ether 1 or 0. 1 in case of a successful read,
## otherwise 0.
##
## If the read was successful, timestamp contains the actual time while
## writing. stats_hash will contain the saved stats.
##
sub read_tmpfile {
    my ($filename) = @_;
    my ( $time, %stats, $statsref );

    if ( !-e $filename ) {
        return 0;
    }

    $statsref = retrieve($filename);
    $np->nagios_exit( 'UNKNOWN', "Unable to retrieve stats from: $filename\n" )
      unless defined $statsref;

    %stats = %$statsref;

    return ( 1, %stats );
}

sub get_interface_stats {
    if ( $^O eq 'linux' ) {
        return get_interface_stats_linux();
    }
    elsif ( $^O =~ /bsd$/ || $^O eq 'darwin' ) {
        return get_interface_stats_bsd();
    }
    else {
        $np->nagios_exit( 'UNKNOWN', "Unsupported OS: $^O" );
    }
}

sub get_interface_stats_linux {

    my %interfaces;

    open( my $stats, "<", $proc )
      or $np->nagios_exit( 'UNKNOWN', "Could not open $proc" );

    while (<$stats>) {
        chomp;
        next unless /^\s+([\w\d]+): (.*)/;
        my $interface = $1;
        my (
            $rxbytes, $rxpackets, $rxerrs,       $rxdrop,
            $rxfifo,  $rxframe,   $rxcompressed, $rxmulticast,
            $txbytes, $txpackets, $txerrs,       $txdrop,
            $txfifo,  $txcolls,   $txcarrier,    $txcompressed
        ) = split( ' ', $2 );
        $interfaces{$interface} = {
            rxbytes      => $rxbytes,
            rxpackets    => $rxpackets,
            rxerrs       => $rxerrs,
            rxdrop       => $rxdrop,
            rxfifo       => $rxfifo,
            rxframe      => $rxframe,
            rxcompressed => $rxcompressed,
            rxmulticast  => $rxmulticast,
            txbytes      => $txbytes,
            txpackets    => $txpackets,
            txerrs       => $txerrs,
            txdrop       => $txdrop,
            txfifo       => $txfifo,
            txcolls      => $txcolls,
            txcarrier    => $txcarrier,
            txcompressed => $txcompressed
        };
    }

    return %interfaces;
}

sub get_interface_stats_bsd {

    my %interfaces;

    open( my $stats, "-|", $netstat )
      or $np->nagios_exit( 'UNKNOWN', "Could not call $netstat" );

    while (<$stats>) {
        chomp;
        next unless /Link/;
        my (
            $interface, $mtu,     $network, $address,   $rxpackets,
            $rxerrs,    $rxdrop,  $rxbytes, $txpackets, $txerrs,
            $txbytes,   $txcolls, $txdrop
        );
        if ( $^O =~ /bsd$/ ) {
            (
                $interface, $mtu,     $network, $address,   $rxpackets,
                $rxerrs,    $rxdrop,  $rxbytes, $txpackets, $txerrs,
                $txbytes,   $txcolls, $txdrop
            ) = split( ' ', $_ );
            if ( $address !~ /:/ ) {
                (
                    $interface, $mtu,     $network, $rxpackets,
                    $rxerrs,    $rxdrop,  $rxbytes, $txpackets,
                    $txerrs,    $txbytes, $txcolls, $txdrop
                ) = split( ' ', $_ );
            }
        }
        elsif ( $^O = 'darwin' ) {
            (
                $interface, $mtu,     $network, $address,
                $rxpackets, $rxerrs,  $rxbytes, $txpackets,
                $txerrs,    $txbytes, $txcolls, $txdrop
            ) = split( ' ', $_ );
            if ( $address !~ /:/ ) {
                (
                    $interface, $mtu,     $network,   $rxpackets,
                    $rxerrs,    $rxbytes, $txpackets, $txerrs,
                    $txbytes,   $txcolls, $txdrop
                ) = split( ' ', $_ );
            }
        }
        if ( $interface =~ /\*$/ ) { next; }
        $interfaces{$interface} = {
            rxbytes      => $rxbytes,
            rxpackets    => $rxpackets,
            rxerrs       => $rxerrs,
            rxdrop       => $rxdrop,
            rxfifo       => '',
            rxframe      => '',
            rxcompressed => '',
            rxmulticast  => '',
            txbytes      => $txbytes,
            txpackets    => $txpackets,
            txerrs       => $txerrs,
            txdrop       => $txdrop,
            txfifo       => '',
            txcolls      => $txcolls,
            txcarrier    => '',
            txcompressed => ''
        };
    }

    return %interfaces;
}

sub convert_units {
    my ( $bytes, $unit ) = @_;
    if ( $unit =~ /^b/ ) {
        $unit = 'N';
    }
    if ($has_number_format) {
        return Number::Format::unformat_number(
            Number::Format::format_bytes( $bytes, unit => $unit ) );
    }
    else {
        return $bytes;
    }
}

my %old_stats;

# If a tmpfile is defined, we (hopefully) get old_stats
# from that file. Otherwise we have to take our own
# stats "old_stats".
if ( defined $np->opts->tmpfile ) {
    my $rc;

    ( $rc, %old_stats ) = read_tmpfile($tmpfile);

    if ( $rc eq 0 ) {

        # We could not open the old stats file, take a
        # new one an quit with UNKNOWN State.

        write_tmpfile( $tmpfile, time, get_interface_stats() );
        $np->nagios_exit( 'UNKNOWN', "Could not open tmpfile: $tmpfile" );
    }

    $sleep = time - $old_stats{'time'};

    # We need to check if $sleep is zero to prevent
    # "devision by zero" errors.
    if ( $sleep eq 0 ) {
        $sleep = 1;
    }
}
else {
    %old_stats = get_interface_stats();
    $sleep     = $np->opts->sleep;
    sleep($sleep);
}

my %new_stats = get_interface_stats();

# We need to check, if a tmpfile sould be written to disk.
if ( defined $np->opts->tmpfile ) {
    write_tmpfile( $tmpfile, time, %new_stats );
}

my $check_interfaces;
if ( $np->opts->interface ) {
    $check_interfaces = $np->opts->interface;
}
else {
    $check_interfaces = [ grep( !/^lo/, keys %new_stats ) ];
}

foreach my $interface (@$check_interfaces) {
    my $rxbytes =
      ( $new_stats{$interface}{rxbytes} - $old_stats{$interface}{rxbytes} ) /
      $sleep;
    my $txbytes =
      ( $new_stats{$interface}{txbytes} - $old_stats{$interface}{txbytes} ) /
      $sleep;
    my $rxerrs =
      ( $new_stats{$interface}{rxerrs} - $old_stats{$interface}{rxerrs} ) /
      $sleep;
    my $txerrs =
      ( $new_stats{$interface}{txerrs} - $old_stats{$interface}{txerrs} ) /
      $sleep;

    $rxbytes = convert_units( $rxbytes, $np->opts->units );
    $txbytes = convert_units( $txbytes, $np->opts->units );

    if ( $np->opts->critrxbytes > 0 && $rxbytes > $np->opts->critrxbytes ) {
        $np->add_message( CRITICAL,
            "$interface RX: $rxbytes " . $np->opts->units . "/s" );
    }
    elsif ( $np->opts->warnrxbytes > 0 && $rxbytes > $np->opts->warnrxbytes ) {
        $np->add_message( WARNING,
            "$interface RX: $rxbytes " . $np->opts->units . "/s" );
    }
    elsif ( $np->opts->verbose ) {
        $np->add_message( OK,
            "$interface RX: $rxbytes " . $np->opts->units . "/s" );
    }

    if ( $np->opts->crittxbytes > 0 && $txbytes > $np->opts->crittxbytes ) {
        $np->add_message( CRITICAL,
            "$interface TX: $txbytes " . $np->opts->units . "/s" );
    }
    elsif ( $np->opts->warntxbytes > 0 && $txbytes > $np->opts->warntxbytes ) {
        $np->add_message( WARNING,
            "$interface TX: $txbytes " . $np->opts->units . "/s" );
    }
    elsif ( $np->opts->verbose ) {
        $np->add_message( OK,
            "$interface TX: $txbytes " . $np->opts->units . "/s" );
    }

    if ( $np->opts->criterrs > 0 && $rxerrs > $np->opts->criterrs ) {
        $np->add_message( CRITICAL, "$interface RX-ERRS: $rxerrs errs/s" );
    }
    elsif ( $np->opts->warnerrs > 0 && $rxerrs > $np->opts->warnerrs ) {
        $np->add_message( WARNING, "$interface RX-ERRS: $rxerrs errs/s" );
    }
    elsif ( $np->opts->verbose ) {
        $np->add_message( OK, "$interface RX-ERRS: $rxerrs errs/s" );
    }

    if ( $np->opts->criterrs > 0 && $txerrs > $np->opts->criterrs ) {
        $np->add_message( CRITICAL, "$interface TX-ERRS: $txerrs errs/s" );
    }
    elsif ( $np->opts->warnerrs > 0 && $txerrs > $np->opts->warnerrs ) {
        $np->add_message( WARNING, "$interface TX-ERRS: $txerrs errs/s" );
    }
    elsif ( $np->opts->verbose ) {
        $np->add_message( OK, "$interface TX-ERRS: $txerrs errs/s" );
    }

    $np->add_perfdata(
        label    => $interface . '-rx',
        value    => $rxbytes,
        uom      => $np->opts->units . "/s",
        warning  => $np->opts->warnrxbytes,
        critical => $np->opts->critrxbytes,
    );

    $np->add_perfdata(
        label    => $interface . '-tx',
        value    => $txbytes,
        uom      => $np->opts->units . "/s",
        warning  => $np->opts->warntxbytes,
        critical => $np->opts->crittxbytes,
    );

    $np->add_perfdata(
        label    => $interface . '-rxerrs',
        value    => $rxerrs,
        uom      => "errs/s",
        warning  => $np->opts->warnerrs,
        critical => $np->opts->criterrs,
    );

    $np->add_perfdata(
        label    => $interface . '-txerrs',
        value    => $txerrs,
        uom      => "errs/s",
        warning  => $np->opts->warnerrs,
        critical => $np->opts->criterrs,
    );
}

my $code;
my $message;
( $code, $message ) = $np->check_messages;

$np->nagios_exit( $code, $message );
